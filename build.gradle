allprojects {
	group = 'com.nsxwing.common'
	version = '0.1.0'

	apply plugin: 'maven'
	apply plugin: 'signing'

	repositories {
		mavenCentral()
		jcenter()
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'maven-publish'

	tasks.withType(JavaCompile) {
		sourceCompatibility = '1.8'
		targetCompatibility = '1.8'

		options.fork = true
		options.encoding = "UTF-8"
	}

	dependencies {
		compile "org.projectlombok:lombok:${project.lombokVersion}"
		compile "org.slf4j:slf4j-api:${project.slf4jVersion}"


		testCompile "org.slf4j:log4j-over-slf4j:${project.slf4jVersion}"
		testCompile "org.slf4j:jcl-over-slf4j:${project.slf4jVersion}"
		testCompile "org.mockito:mockito-core:${project.mockitoVersion}"
		testCompile "junit:junit:${project.junitVersion}"
	}

	task sourceJar(type: Jar) {
		classifier = 'sources'
		from sourceSets.main.allJava
	}

	task javadocJar(type: Jar) {
		classifier = 'javadoc'
		from javadoc
	}

	artifacts {
		archives javadocJar, sourceJar
	}

	signing {
		sign configurations.archives
	}


	publishing {
		publications {
			mavenJava(MavenPublication) {
				from components.java
				artifact sourceJar
			}
		}
	}

	uploadArchives {
		repositories {
			mavenDeployer {
				beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

				repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
					authentication(userName: ossrhUsername, password: ossrhPassword)
				}

				snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
					authentication(userName: ossrhUsername, password: ossrhPassword)
				}

				pom.project {
					name project.name
					packaging 'jar'
					artifactId project.name
					description project.description
					url 'https://github.com/CGreff/nsx-wing-common'

					scm {
						connection 'scm:git:git@github.com:CGreff/nsx-wing-common.git'
						developerConnection 'scm:git:git@github.com:CGreff/nsx-wing-common.git'
						url 'https://github.com/CGreff/nsx-wing-common'
					}

					licenses {
						license {
							name 'MIT License'
							url 'http://www.opensource.org/licenses/mit-license.php'
						}
					}

					developers {
						developer {
							name 'Christopher Greff'
							email 'chris@nsxwing.com'
						}
					}
				}
			}
		}
	}
}

